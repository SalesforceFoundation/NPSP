/*
    Copyright (c) 2018, Salesforce.org
    All rights reserved.

    Redistribution and use in source and binary forms, with or without
    modification, are permitted provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright
      notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright
      notice, this list of conditions and the following disclaimer in the
      documentation and/or other materials provided with the distribution.
    * Neither the name of Salesforce.org nor the names of
      its contributors may be used to endorse or promote products derived
      from this software without specific prior written permission.

    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
    "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
    LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
    FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
    COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
    INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
    BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
    LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
    CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
    LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
    ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
    POSSIBILITY OF SUCH DAMAGE.
*/
/**
* @author Salesforce.org
* @date 2018
* @group Utilities
* @description A more controllable method to handle the asynchronous submission of Telemetry data to the LMO
* without having to worry about governor limits when it comes to querying large datasets in LDV orgs. By iterating
* through a custom List<> of Enum Types with a batch size of ONE, the execute() method can call a service method
* to handle querying and submitting the specific telemetry data only for that Enum type. If for some reason that
* one query were to fail, which is theoretically possible in a huge organization where there may be other jobs
* executing or other factors at play, the rest of this job will continue. That ensures that our LMO collects as much
* data as possible. In addition, it can help us identify which specific telemetry data processes are failing.
* Class is virtual so executeTelemetrySObjectBatch can be overridden during tests
*/
public without sharing virtual class UTIL_OrgTelemetry_BATCH implements Database.Batchable<UTIL_OrgTelemetry_SVC.TelemetryBatchCategory> {

    @TestVisible
    private UTIL_FeatureManagement featureManager = new UTIL_FeatureManagement();

    /**
     * @description List of High Level Telemetry Data Types to be processed asynchronously by this batch job.
     */
    private final List<UTIL_OrgTelemetry_SVC.TelemetryBatchCategory> telemetryTypes =
            new List<UTIL_OrgTelemetry_SVC.TelemetryBatchCategory>{
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.NPSP_Settings,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Org_Environment,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_MaxNumRelatedOpps,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_CountRdOpps,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_CountRecurringDonations,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_CountErrorLog,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_CountPaymentOpps,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_CountBDIRows,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_CountBGERows,
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_CountGERowsLast30DaysBatch
            };

    /**
     * @description Return the list of TelemetryType enum values to be processed by this batch job
     * @param BC BatchableContext (not used)
     * @return TelemetryTypes list from above
     */
    public Iterable<UTIL_OrgTelemetry_SVC.TelemetryBatchCategory> start(Database.BatchableContext BC) {
        if (!Test.isRunningTest()) {
            return telemetryTypes;
        } else {
            return new List<UTIL_OrgTelemetry_SVC.TelemetryBatchCategory>{
                    UTIL_OrgTelemetry_SVC.TelemetryBatchCategory.Data_CountErrorLog
            };
        }
    }

    /**
     * @description Process the specific set of FeatureParameter submissions to the LMO based on the Telemetry
     * Type Enum value.
     * @param info BatchableContext (not used)
     * @param types List<UTIL_OrgTelemetry_SVC.TelemetryTypeEnum> -- Batch Size of ONE is required
     */
    public void execute(Database.BatchableContext info, List<UTIL_OrgTelemetry_SVC.TelemetryBatchCategory> types) {
        UTIL_OrgTelemetry_SVC telemetrySvc = new UTIL_OrgTelemetry_SVC();
        telemetrySvc.processTelemetryType(types[0]);
    }

    /**
    * @description When the main batch job is done, kick off the secondary batch job to count the
    * number of RecurringDonation Installment Opportunities.
    */
    public void finish(Database.BatchableContext info) {
        handleOpportunityBatchExecution();
        handleRecurringDonationExecution();
        handleDataImportExecution();
    }

    /**
    * @description Handles execution of batch job for opportunity counts
    */
    private void handleOpportunityBatchExecution() {
        Boolean hasRecurringDonations = [SELECT Id FROM npe03__Recurring_Donation__c LIMIT 1].isEmpty() == false;
        Boolean hasPayments = [SELECT Id FROM npe01__OppPayment__c LIMIT 1].isEmpty() == false;

        if (hasRecurringDonations || hasPayments) {
            executeTelemetrySObjectBatch(
                    UTIL_OrgTelemetry_SObject_BATCH.TelemetryBatchJobMode.OPPORTUNITIES,
                    UTIL_OrgTelemetry_SObject_BATCH.OPPORTUNITIES_BATCH_SIZE
            );
        } else {
            featureManager.setPackageIntegerValue(
                    UTIL_OrgTelemetry_SVC.TelemetryParameterName.Data_CountRdOppsAll.name(), 0);
            featureManager.setPackageIntegerValue(
                    UTIL_OrgTelemetry_SVC.TelemetryParameterName.Data_CountRdOppsOpenEnded.name(), 0);

            featureManager.setPackageIntegerValue(
                    UTIL_OrgTelemetry_SVC.TelemetryParameterName.Data_CountOppsWithMultiplePayments.name(), 0);
            /* TODO: Holding on Data_OppPaymentsWithoutPaymentsEnabled metric, reenable test when
                 you move forward with Data_OppPaymentsWithoutPaymentsEnabled */
            //featureManager.setPackageIntegerValue(UTIL_OrgTelemetry_SVC.TelemetryParameterName.Data_OppPaymentsWithoutPaymentsEnabled.name(), 0);
        }
    }

    /**
    * @description Handles execution of batch job for recurring donations counts
    */
    private void handleRecurringDonationExecution() {
        Boolean hasRecurringDonations = [SELECT Id FROM npe03__Recurring_Donation__c LIMIT 1].isEmpty() == false;

        if (hasRecurringDonations) {
            executeTelemetrySObjectBatch(
                    UTIL_OrgTelemetry_SObject_BATCH.TelemetryBatchJobMode.RECURRING_DONATIONS,
                    UTIL_OrgTelemetry_SObject_BATCH.RECURRING_DONATIONS_BATCH_SIZE
            );
        } else {
            featureManager.setPackageIntegerValue(
                    UTIL_OrgTelemetry_SVC.TelemetryParameterName.Data_CountRecurringDonationsAll.name(), 0);
            featureManager.setPackageIntegerValue(
                    UTIL_OrgTelemetry_SVC.TelemetryParameterName.Data_CountRdsWithDiffAmount.name(), 0);
        }
    }

    /**
    * @description Handles execution of batch job for recurring donations counts
    */
    private void handleDataImportExecution() {
        Boolean hasDataImports = [SELECT Id FROM DataImport__c LIMIT 1].isEmpty() == false;

        if (hasDataImports) {
            executeTelemetrySObjectBatch(
                    UTIL_OrgTelemetry_SObject_BATCH.TelemetryBatchJobMode.GE_BATCH_DATA_IMPORTS,
                    UTIL_OrgTelemetry_SObject_BATCH.DATA_IMPORTS_BATCH_SIZE
            );
        } else {
            featureManager.setPackageIntegerValue(
                    UTIL_OrgTelemetry_SVC.TelemetryParameterName.Data_CountGERowsLast30DaysBatch.name(), 0);
        }
    }

    /**
    * @description Executes Telemetry SObject Batch with given mode and size
    */
    @TestVisible
    private virtual void executeTelemetrySObjectBatch(
            UTIL_OrgTelemetry_SObject_BATCH.TelemetryBatchJobMode mode, Integer batchSize) {
        Database.executeBatch(new UTIL_OrgTelemetry_SObject_BATCH(mode), batchSize);
    }
}