/*
    Copyright (c) 2016, Salesforce.org
    All rights reserved.
    
    Redistribution and use in source and binary forms, with or without
    modification, are permitted provided that the following conditions are met:
    
    * Redistributions of source code must retain the above copyright
      notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright
      notice, this list of conditions and the following disclaimer in the
      documentation and/or other materials provided with the distribution.
    * Neither the name of Salesforce.org nor the names of
      its contributors may be used to endorse or promote products derived
      from this software without specific prior written permission.
 
    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
    "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT 
    LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS 
    FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE 
    COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, 
    INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, 
    BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
    LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER 
    CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT 
    LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN 
    ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE 
    POSSIBILITY OF SUCH DAMAGE.
*/
/**
* @author Salesforce.org
* @date 2016
* @group Levels
* @description Batch class processes all Levels against Target objects, and updates any changes.
*/
public without sharing class LVL_LevelAssign_BATCH implements Database.Batchable<sObject>, Database.Stateful {
    
    /** @description The query for the batch process to run on.*/
    private String strSoql;

    /** @description The Object name that we are running the batch process on.*/
    private String strObject;
    
    /** @description The Ladder Levels to run on this object */
    private map<String, list<Level__c>> mapLadderToLevels;
    
    /*********************************************************************************************************
    * @description The batch process constructor; creates opportunity query for all opportunities.
    */
    public LVL_LevelAssign_BATCH(String strSoql, String strObject) {
        this.strSoql = strSoql;
        this.strObject = strObject;

        // get our map of Ladder Levels
        this.mapLadderToLevels = mapLadderToLevelsForObject(strObject);
    }
    
    /*********************************************************************************************************
    * @description Batch process start method.
    */
    public Database.QueryLocator start(Database.BatchableContext BC) {
        return Database.getQueryLocator(strSoql);
    }

    /*********************************************************************************************************
    * @description Batch process execute method. Goes thru each object, and evaluates it against each 
    * ladder, and only updates it if levels change.
    */
    public void execute(Database.BatchableContext BC, List<sObject> scope) {
        
        // track our objects to update
        Map<Id, SObject> mapSObjUpdate = new Map<Id, SObject>();
        
        // track our Engagement Plans to create
        List<Engagement_Plan__c> newEngagementPlans = new list<Engagement_Plan__c>();
        List<Error__c> errorsCreatingEngagementPlans = new List<Error__c>();
        
        // for each of our objects
        for (SObject sobj : scope) {
            
            // for each of our Ladders
            for (String strLadder : mapLadderToLevels.keySet()) {
                list<Level__c> listLvls = mapLadderToLevels.get(strLadder);
                Id lvlIdPrev;
                // track whether or not a match was found
                Boolean matchedLvl = false;
                // note that we have try/catches around using the level fields, so we can provide useful errors if they are invalid

                // go thru each list of Levels finding where this object fits
                for (Level__c lvl : listLvls) {
                    if (lvlIdPrev == null) {
                        try {
                            lvlIdPrev = (Id)sobj.get(strLadder);
                        } catch (Exception ex) {
                            throw new LevelException(String.format(System.Label.lvlErrorInvalidLookupField, new list<String>{strLadder, lvl.Name}));
                        }
                    }
                    Decimal amt;
                    try {
                        amt = (Decimal)sobj.get(lvl.Source_Field__c);
                    } catch (Exception ex) {
                        throw new LevelException(String.format(System.Label.lvlErrorInvalidSourceField, new list<String>{lvl.Source_Field__c, lvl.Name}));
                    }
                    if (amt == null) 
                        amt = 0;

                    if ((amt >= lvl.Minimum_Amount__c || lvl.Minimum_Amount__c == null) && 
                        (amt < lvl.Maximum_Amount__c || lvl.Maximum_Amount__c == null)) {
                        
                        // record the new level
                        matchedLvl = true;
                        if (lvlIdPrev != lvl.Id) {

                            populatePreviousLevelField(sobj, lvl, lvlIdPrev);
                            
                            try {
                                sobj.put(strLadder, lvl.Id);
                            } catch (exception ex) {
                                throw new LevelException(String.format(System.Label.lvlErrorInvalidLookupField, new list<String>{strLadder, lvl.Name}));
                            }
                            
                            mapSObjUpdate.put(sobj.Id, sobj);
                            
                            // apply the Engagement Plan
                            if (lvl.Engagement_Plan_Template__c != null) {
                                Engagement_Plan__c engagementPlanFromLevel = engagementPlanFromLevel(lvl, sobj.Id);
                                if (engagementPlanFromLevel != null) {
                                    newEngagementPlans.add(engagementPlanFromLevel);
                                } else {
                                    errorsCreatingEngagementPlans.add(
                                        new Error__c(
                                            Datetime__c = System.now(),
                                            Context_Type__c = ERR_Handler_API.Context.LVL.name(),
                                            Error_Type__c = 'Batch Apex error',
                                            Full_Message__c = (System.Label.engagementPlanMissingField + ' ' + lvl.Target__c)
                                        )
                                    );
                                }
                            }
                        }
                        // done with this level.
                        break;
                    }

                } // levels
                // After looping through all levels in the current ladder if no match found but level set clear level
                if (!matchedLvl && lvlIdPrev != null) {
                    //Use previous field's level to find which Previous_Level_Field__c needs to be populated
                    Level__c previousLevel = (new Map<Id, Level__c>(listLvls)).get(lvlIdPrev);

                    populatePreviousLevelField(sobj, previousLevel, lvlIdPrev);

                    sobj.put(strLadder, null);
                    mapSObjUpdate.put(sobj.Id, sobj);
                }
            } // ladder            
        } // scope
                
        // save 'em
        update mapSObjUpdate.values();     
        if (!newEngagementPlans.isEmpty()) {
            insert newEngagementPlans;
        }
        if (!errorsCreatingEngagementPlans.isEmpty()) {
            insert errorsCreatingEngagementPlans;
        }
    }

    /*******************************************************************************************************
    * @description Creates an Engagement Plan based on a Level record.
    * Attempts to find the engagement plan lookup field based on the level's target field
    * it could be either a standard object that requires us to add a namespace prefix and custom field suffix
    * e.g. Account -> npsp__Account__c, Contact -> npsp__Contact__c
    * or a standard object added by a user with no prefix that requires us to add a custom feild suffix
    * e.g. Case -> Case__c
    * or a custom object name without namespace prefix and with suffix already appended
    * e.g. Custom_Object__c -> Custom_Object__c
    * @param level The level used to create the Engagement Plan
    * @param target The Id of object to relate the Engagement Plan to
    * @return Engagement_Plan__c The Engagement Plan
    */
    private Engagement_Plan__c engagementPlanFromLevel(Level__c level, Id target) {
        Engagement_Plan__c ep = new Engagement_Plan__c(Engagement_Plan_Template__c = level.Engagement_Plan_Template__c);
        
        String engagementPlanPrefixed = UTIL_Namespace.StrTokenNSPrefix('Engagement_Plan__c');
        String targetFieldPrefixedAndSuffixed = UTIL_Namespace.StrTokenNSPrefix(level.Target__c + '__c');
        String targetFieldSuffixed = level.Target__c + '__c';
        
        if (UTIL_Describe.isValidField(engagementPlanPrefixed, targetFieldPrefixedAndSuffixed)) {
            ep.put(targetFieldPrefixedAndSuffixed, target);
        
        } else if (UTIL_Describe.isValidField(engagementPlanPrefixed, targetFieldSuffixed)) {
            ep.put(targetFieldSuffixed, target);
        
        } else if (UTIL_Describe.isValidField(engagementPlanPrefixed, level.Target__c)) {
            ep.put(level.Target__c, target);
        
        } else {
            //no matching field found, can't create an engagement plan
            return null;
        }
        return ep;
    }

    /*******************************************************************************************************
    * @description Populates Sobject's Previous Level field with a level previously stored in the Level Field 
    * @param sobj Sobject
    * @param level The level used to find out the Sobject's Previous Level Field
    * @param prevLevelId An Id of a level previously specified in the Level Field
    * @return void
    */
    private void populatePreviousLevelField(Sobject sobj, Level__c level, Id prevLevelId) {
        if (level == null || level.Previous_Level_Field__c == null) {
            return;
        }
        
        try {
            sobj.put(level.Previous_Level_Field__c, prevLevelId);

        } catch(Exception ex) {
            throw new LevelException(
                String.format(
                    System.Label.lvlErrorInvalidLookupField, 
                    new List<String>{ level.Previous_Level_Field__c, level.Name }
                )
            );
        }
    }
    
    private class LevelException extends Exception {}
    
    /*********************************************************************************************************
    * @description Batch process finish method, does nothing.
    */
    public void finish(Database.BatchableContext BC) {}
    
    /*********************************************************************************************************
    * @description For the given object, return a map from it's different Ladders (Level_Field__c), to the
    * appropriate Levels for that Ladder. The levels are stored in increasing order for Minimum Amount.
    * @param strObj The object to get the ladder levels for
    * @return map<String, list<Level__c>> The map of Ladder Levels for the object
    */
    public static map<String, list<Level__c>> mapLadderToLevelsForObject(String strObj) {
        
        // get all our levels for this object type
        list<Level__c> listAllLvls = [select Id, Name, Target__c, Source_Field__c, Level_Field__c, 
            Previous_Level_Field__c, Active__c, Minimum_Amount__c, Maximum_Amount__c, Engagement_Plan_Template__c
            from Level__c
            where Target__c = :strObj and Active__c = true
            order by Level_Field__c asc, Minimum_Amount__c asc nulls first];
            
        // create our map to store the list of Levels per Ladder
        map<String, list<Level__c>> mapLadderToLvls = new map<String, list<Level__c>>();
        
        for (Level__c lvl : listAllLvls) {
            string ladder = lvl.Level_Field__c;
            list<Level__c> listLvls = mapLadderToLvls.get(ladder);
            if (listLvls == null) {
                listLvls = new list<Level__c>();
                mapLadderToLvls.put(ladder, listLvls);
            }
            listLvls.add(lvl);
        }
        
        // we now have a map from Ladder to its list of Levels
        return mapLadderToLvls;         
    }
    
    /*********************************************************************************************************
    * @description query the Level object, and return a map of Object Name to a Set of Fields to query.
    * @return map<String, list<String>> The map of Object Name to its Set of Fields
    */
    public static map<String, set<String>> getMapStrObjectToQueryFields() {
        
        // get all our levels
        list<Level__c> listAllLvls = [select Id, Name, Target__c, Source_Field__c, Level_Field__c, 
            Previous_Level_Field__c, Active__c
            from Level__c
            where Active__c = true
            order by Target__c, Level_Field__c];
            
        // create our map
        map<String, set<String>> mapStrObjectToQueryFields = new map<String, set<String>>();
        
        for (Level__c lvl : listAllLvls) {
            // for each object, we'll add all its source, level, and previous level fields
            String strObj = lvl.Target__c;
            Set<String> setFlds = mapStrObjectToQueryFields.get(strObj);
            if (setFlds == null) {
                setFlds = new Set<String>();
                mapStrObjectToQueryFields.put(strObj, setFlds);
            }
            setFlds.add(lvl.Source_Field__c);
            setFlds.add(lvl.Level_Field__c);
            if (lvl.Previous_Level_Field__c != null)
                setFlds.add(lvl.Previous_Level_Field__c);            
        }
        
        return mapStrObjectToQueryFields; 
    }
}